{if;{logic;||;{bool;{lower;{args;0}};==;help};{flagset;h}};
  {//;drpg help <sub-tag> OR drpg -h <sub-tag>}
  {if;{argslength};>;1;
    {exec;drpg;{args;1};help}
    {return;false}
  ;
    {if;{length;{flag;h}};>;0;
      {exec;drpg;{flag;h};help}
      {return;false}
    }
  }

  {embed;{buildembed;title:List of useful drpg tags;
  color:{exec;dcolor};
  fields.name:Usage;
  fields.value:{unindent;
    `b!t drpg <sub-tag> [args]`
    `<sub-tag>` can be any of the sub-tags specified below, and `[args]` depends on the sub-tag used.

  };
  fields.name:Sub-tags;
  fields.value:{unindent;
    *Displays user level and required XP:*
    - `b!t drpg level [user]` or
    - `b!t drpg lvl` (alias of `b!t drpg.level`)

    *Displays pet info and required XP:*
    - `b!t drpg p [user]` or
    - `b!t drpg pet [user]`

    *Displays pet level and required XP:*
    - `b!t drpg plvl [user]` or
    - `b!t drpg petlvl [user]` (alias of `b!t drpg.petlvl`)

    *Displays or changes the configured shortcut:*
    - `b!t drpg shortcut [newshortcut] [appendArgs]`

    For info on a sub-tag use `b!t drpg <sub-tag> help` or `b!t drpg help <sub-tag>`
    **Special thanks to Dx8pi for creating `b!t nlvl` and `b!t petlvl`**
  }}}
{return;false}
}
{if;{argslength};>;1;
    {set;~args;{args;1;n}}
;
    {//;Nothing}
}
{switch;{lower;{args;0}};
  ["lvl","level"];
    {exec;drpg.level;{get;~args}}
    {return;false}
  ;["plvl","petlvl","petlevel","plevel"];
    {exec;drpg.petlvl;{get;~args}}
    {return;false}
  ;["pet","p"];
    {exec;drpg.pet;{get;~args}}
    {return;false}
  ;shortcut;
    {set;~shortcuts;lvl;level;p;plvl;petlvl;petlevel;plevel;pet}
    {if;{get;~args};==;;
      {embed;{buildembed;
        title:Drpg shortcut;
        description:{unindent;
          {if;{get;@drpg.shortcut.{userid}};==;;
            No shortcut configured
          ;
            Your current shortcut is set to `{get;@drpg.shortcut.{userid}}`
          }

          To change the shortcut use `b!t drpg shortcut <newshortcut> [appendArgs]`, like `b!t drpg shortcut lvl`.
          `[appendArgs]` will be appended to the arguments of the tag (`b!t drpg shortcut lvl RagingLink` will always return the level of RagingLink).
          To reset the shortcut use `b!t drpg shortcut reset`
        };
        color:{exec;dcolor}
      }}
      {return;false}
    }

    {if;{lower;{get;~args}};==;reset;
      {set;@drpg.shortcut.{userid}}
      {set;@drpg.shortcut.args.{userid}}
      Reset your shortcut!
      {return;false}
    }

    {if;{get;~shortcuts};includes;{lower;{args;1}};
      {set;@drpg.shortcut.{userid};{lower;{args;1}}}
      {if;{argslength};>;2;
        {set;@drpg.shortcut.args.{userid};{args;2;n}}
      ;
        {set;@drpg.shortcut.args.{userid}}
      }
      Set shortcut to `{lower;{args;1}}` {if;{get;@drpg.shortcut.args.{userid}};!=;;with `{get;@drpg.shortcut.args.{userid}}` as appended arguments}
      {return;false}
    ;
      `{lower;{args;0}}` is not a valid sub-tag, please pick one from `{join;{get;~shortcuts};,{space}}`.
      {return;false}
    }
  ;
    {if;{get;@drpg.shortcut.{userid}};!=;;
      {if;{argslength};>;0;
        {set;~args;{args;0;n}}
      ;
        {//;Nothing}
      }

      {exec;drpg;{get;@drpg.shortcut.{userid}};{trim;{get;~args}{space}{get;@drpg.shortcut.args.{userid}}}}
      {return;false}
    }
    {if;{argslength};==;0;
      {exec;drpg;help}
    ;
      Unrecognized sub-tag `{args;0}`
    }
  {return;false}
}